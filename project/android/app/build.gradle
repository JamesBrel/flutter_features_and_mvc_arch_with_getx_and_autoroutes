
def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
       }
    }

    namespace = "com.ouitogether.ouitok"
    compileSdk = 35
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId = "com.ouitogether.ouitok"
        minSdk = 16
        targetSdk = 35
        versionCode = flutter.versionCode
        versionName = flutter.versionName
        multiDexEnabled = true
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            jniDebuggable true
            renderscriptDebuggable true
            renderscriptOptimLevel 4
            signingConfig signingConfigs.debug
            minifyEnabled false
        }
    }

    flavorDimensions 'ouitok_env'
    productFlavors {
        dev {
            dimension 'ouitok_env'
            applicationId 'com.ouitogether.ouitok'
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
            resValue 'string', 'app_name', 'OuiTok Dev'
            signingConfig signingConfigs.debug
        }
        staging {
            dimension 'ouitok_env'
            applicationId 'com.ouitogether.ouitok'
            applicationIdSuffix '.staging'
            versionNameSuffix '-staging'
            resValue 'string', 'app_name', 'OuiTok Staging'
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        prod {
            dimension 'ouitok_env'
            applicationId 'com.ouitogether.ouitok'
            resValue 'string', 'app_name', 'OuiTok'
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

flutter {
    source = "../.."
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.android.support:multidex:1.0.3'
}
